\appendix
\chapter{Source code} \label{cha:source}

This appendix contains the source of the controller as given by revision no. 4 to the SVN repository 
\begin{verbatim}//repos.gbar.dtu.dk/mhha/Controller/\end{verbatim}
There are two files, risoe\_controller.f90 and risoe\_controller\_fcns.f90 in this Fortran90 code. This type2 DLL for HAWC2 has been compiled with Intel Fortran 13.0.

\section{Main routines in risoe\_controller.f90}
{\scriptsize
\begin{verbatim}
subroutine init_regulation(array1,array2)
use risoe_controller_fcns
implicit none
!DEC$ ATTRIBUTES DLLEXPORT, C, ALIAS:'init_regulation'::init_regulation
real*8 array1(1000),array2(1)
! Local vars
integer*4 i,ifejl
character text32*32
real*8 minimum_pitch_angle
logical findes
! Input array1 must contain
!
!       ; Overall parameters
!    1: constant   1   ; Rated power [kW]
!    2: constant   2   ; Minimum rotor speed [rad/s]
!    3: constant   3   ; Rated rotor speed [rad/s]
!    4: constant   4   ; Maximum allowable generator torque [Nm]
!    5: constant   5   ; Minimum pitch angle, theta_min [deg],
!                      ; if |theta_min|>90, then a table of <wsp,theta_min> is read
!                      ; from a file named 'wptable.n', where n=int(theta_min)
!    6: constant   6   ; Maximum pitch angle [deg]
!    7: constant   7   ; Maximum pitch velocity operation [deg/s]
!    8: constant   8   ; Frequency of generator speed filter [Hz]
!    9: constant   9   ; Damping ratio of speed filter [-]
!   10: constant  10   ; Frequency of free-free DT torsion mode [Hz], if zero no notch filter used
!       ; Partial load control parameters
!   11: constant  11   ; Optimal Cp tracking K factor [Nm/(rad/s)^2],
!                      ; Qg=K*Omega^2, K=eta*0.5*rho*A*Cp_opt*R^3/lambda_opt^3
!   12: constant  12   ; Proportional gain of torque controller [Nm/(rad/s)]
!   13: constant  13   ; Integral gain of torque controller [Nm/rad]
!   14: constant  14   ; Differential gain of torque controller [Nm/(rad/s^2)]
!       ; Full load control parameters
!   15: constant  15   ; Generator control switch [1=constant power, 2=constant torque]
!   16: constant  16   ; Proportional gain of pitch controller [rad/(rad/s)]
!   17: constant  17   ; Integral gain of pitch controller [rad/rad]
!   18: constant  18   ; Differential gain of pitch controller [rad/(rad/s^2)]
!   19: constant  19   ; Proportional power error gain [rad/W]
!   20: constant  20   ; Integral power error gain [rad/(Ws)]
!   21: constant  21   ; Coefficient of linear term in aerodynamic gain scheduling, KK1 [deg]
!   22: constant  22   ; Coefficient of quadratic term in aerodynamic gain scheduling, KK2 [deg^2]
!                      ; (if zero, KK1 = pitch angle at double gain)
!   23: constant  23   ; Relative speed for double nonlinear gain [-]
!       ; Cut-in simulation parameters
!   24: constant  24   ; Cut-in time [s], if zero no cut-in simulated
!   25: constant  25   ; Time delay for soft start [1/1P]
!       ; Cut-out simulation parameters
!   26: constant  26   ; Cut-out time [s], if zero no cut-out simulated
!   27: constant  27   ; Time constant for 1st order filter lag of torque cut-out [s]
!   28: constant  28   ; Stop type [1=linear two pitch speed stop, 2=exponential pitch speed stop]
!   29: constant  29   ; Time delay for pitch stop 1 [s]
!   30: constant  30   ; Maximum pitch velocity during stop 1 [deg/s]
!   31: constant  31   ; Time delay for pitch stop 2 [s]
!   32: constant  32   ; Maximum pitch velocity during stop 2 [deg/s]
!       ; Expert parameters (keep default values unless otherwise given)
!   33  constant  33   ; Lower angle above lowest minimum pitch angle for switch [deg]
!   34: constant  34   ; Upper angle above lowest minimum pitch angle for switch [deg]
!   35: constant  35   ; Ratio between filtered and reference speed for fully open torque limits [%]
!   36: constant  36   ; Time constant of 1st order filter on wind speed used for minimum pitch [1/1P]
!   37: constant  37   ; Time constant of 1st order filter on pitch angle for gain scheduling [1/1P]
!  	38: constant  38   ; Proportional gain of DT damper [Nm/(rad/s)], requires frequency in input 10
!
! Output array2 contains nothing for init
!
! Overall parameters
Pe_rated                 =array1( 1)*1.d3
omega_ref_min            =array1( 2)
omega_ref_max            =array1( 3)
max_lss_torque           =array1( 4)
minimum_pitch_angle      =array1( 5)*degrad
pitch_stopang            =array1( 6)*degrad
PID_pit_var.velmax       =array1( 7)*degrad
omega2ordervar.f0        =array1( 8)
omega2ordervar.zeta       =array1( 9)
DT_mode_filt.f0          =array1(10)
! Partial load control parameters
Kopt                     =array1(11)
if (Kopt*omega_ref_max**2.ge.Pe_rated/omega_ref_max) Kopt=Pe_rated/omega_ref_max**3
PID_gen_var.Kpro         =array1(12)
PID_gen_var.Kint         =array1(13)
PID_gen_var.Kdif         =array1(14)
! Full load control parameters
const_power         =(int(array1(15)).eq.1)
PID_pit_var.kpro(1)      =array1(16)
PID_pit_var.kint(1)      =array1(17)
PID_pit_var.kdif(1)      =array1(18)
PID_pit_var.kpro(2)      =array1(19)
PID_pit_var.kint(2)      =array1(20)
PID_pit_var.kdif(2)      =0.d0
kk1                      =array1(21)*degrad
kk2                      =array1(22)*degrad*degrad
rel_limit                =array1(23)
! Cut-in simulation parameters
t_cutin                  =array1(24)
t_cutin_delay            =array1(25)*2.d0*pi/omega_ref_max
! Cut-out simulation parameters
t_cutout                 =array1(26)
torquefirstordervar.tau  =array1(27)
pitch_stoptype       =int(array1(28))
pitch_stopdelay          =array1(29)
pitch_stopvelmax         =array1(30)*degrad
pitch_stopdelay2         =array1(31)
pitch_stopvelmax2        =array1(32)*degrad
! Expert parameters (keep default values unless otherwise given)
switch1_pitang_lower     =array1(33)*degrad
switch1_pitang_upper     =array1(34)*degrad
rel_sp_open_Qg           =array1(35)*1.d-2
wspfirstordervar.tau     =array1(36)*2.d0*pi/omega_ref_max
pitchfirstordervar.tau   =array1(37)*2.d0*pi/omega_ref_max
! Drivetrain damper
DT_damp_gain             =array1(38)
DT_damper_filt.f0        =DT_mode_filt.f0
pwr_DT_mode_filt.f0      =DT_mode_filt.f0
! Default and derived parameters
PID_gen_var.velmax=0.d0 !No limit to generator torque change rate
Qg_rated=Pe_rated/omega_ref_max
switchfirstordervar.tau=2.d0*pi/omega_ref_max
cutinfirstordervar.tau=2.d0*pi/omega_ref_max
! Wind speed table
if (dabs(minimum_pitch_angle).lt.90.d0*degrad) then
  Opdatavar.lines=2
  Opdatavar.wpdata(1,1)=0.d0
  Opdatavar.wpdata(2,1)=99.d0
  Opdatavar.wpdata(1,2)=minimum_pitch_angle
  Opdatavar.wpdata(2,2)=minimum_pitch_angle
else
  write(text32,'(i)') int(minimum_pitch_angle*raddeg)
  inquire(file='wpdata.'//trim(adjustl(text32)),exist=findes)
  if (findes) then
    open(88,file='wpdata.'//trim(adjustl(text32)))
    read(88,*,iostat=ifejl) Opdatavar.lines
    if (ifejl.eq.0) then
      do i=1,Opdatavar.lines
        read(88,*,iostat=ifejl) Opdatavar.wpdata(i,1),Opdatavar.wpdata(i,2)
        if (ifejl.ne.0) then
          write(6,*) ' *** ERROR *** Could not read lines in minimum '&
                   //'pitch table in file wpdata.'//trim(adjustl(text32))
          stop
        endif
        Opdatavar.wpdata(i,2)=Opdatavar.wpdata(i,2)*degrad
      enddo
    else
      write(6,*) ' *** ERROR *** Could not read number of lines '&
               //'in minimum pitch table in file wpdata.'//trim(adjustl(text32))
      stop
    endif
    close(88)
  else
    write(6,*) ' *** ERROR *** File ''wpdata.'//trim(adjustl(text32))&
             //''' does not exist in the working directory'
    stop
  endif
endif
! Initiate the dynamic variables
stepno=0
time_old=0.d0
! No output
array2=0.d0
return
end subroutine init_regulation
!**************************************************************************************************
subroutine update_regulation(array1,array2)
use risoe_controller_fcns
implicit none
!DEC$ ATTRIBUTES DLLEXPORT, C, ALIAS:'update_regulation'::update_regulation
real*8 array1(1000),array2(100)
! Input array1 must contain
!
!    1: general time                           ; [s]
!    2: constraint bearing1 shaft_rot 1 only 2 ; [rad/s] Generator LSS speed
!    3: constraint bearing2 pitch1 1 only 1    ; [rad]
!    4: constraint bearing2 pitch2 1 only 1    ; [rad]
!    5: constraint bearing2 pitch3 1 only 1    ; [rad]
!  6-8: wind free_wind 1 0.0 0.0 hub height    ; [m/s] global coords at hub height
!
! Output array2 contains
!
!    1: Generator torque reference            [Nm]
!    2: Pitch angle reference of blade 1      [rad]
!    3: Pitch angle reference of blade 2      [rad]
!    4: Pitch angle reference of blade 3      [rad]
!    5: Power reference                       [W]
!    6: Filtered wind speed                   [m/s]
!    7: Filtered rotor speed                  [rad/s]
!    8: Filtered rotor speed error for torque [rad/s]
!    9: Bandpass filtered rotor speed         [rad/s]
!   10: Proportional term of torque contr.    [Nm]
!   11: Integral term of torque controller    [Nm]
!   12: Minimum limit of torque               [Nm]
!   13: Maximum limit of torque               [Nm]
!   14: Torque limit switch based on pitch    [-]
!   15: Filtered rotor speed error for pitch  [rad/s]
!   16: Power error for pitch                 [W]
!   17: Proportional term of pitch controller [rad]
!   18: Integral term of pitch controller     [rad]
!   19: Minimum limit of pitch                [rad]
!   20: Maximum limit of pitch                [rad]
!   21: Torque reference from DT damper       [Nm]
!
! Local variables
real*8 time,omega,omegafilt,domega_dt_filt,wsp,WSPfilt
real*8 omega_err_filt_pitch,omega_err_filt_speed,omega_dtfilt
real*8 ommin1,ommin2,ommax1,ommax2
real*8 pitang(3),meanpitang,meanpitangfilt,theta_min,e_pitch(2)
real*8 kgain_pitch(3,2),kgain_torque(3),aero_gain,x,dummy,y(2)
real*8 Qg_min_partial,Qg_max_partial,Qg_min_full,Qg_max_full
real*8 Qgen_ref,theta_col_ref,thetaref(3),Pe_ref,Qdamp_ref
!**************************************************************************************************
! Increment time step (may actually not be necessary in type2 DLLs)
!**************************************************************************************************
time=array1(1)
if (time.gt.time_old) then
  deltat=time-time_old
  time_old=time
  stepno=stepno+1
endif
!**************************************************************************************************
! Inputs and their filtering
!**************************************************************************************************
omega=array1(2)
! Mean pitch angle
pitang(1)=array1(3)
pitang(2)=array1(4)
pitang(3)=array1(5)
meanpitang=(pitang(1)+pitang(2)+pitang(3))/3.d0
! Wind speed as horizontal vector sum
wsp=dsqrt(array1(6)**2+array1(7)**2)
! Low-pass filtering of the rotor speed
y=lowpass2orderfilt(deltat,stepno,omega2ordervar,omega)
omegafilt=y(1)
domega_dt_filt=y(2)
! Mean pitch angle
! Low-pass filtering of the mean pitch angle for gain scheduling
meanpitangfilt=min(lowpass1orderfilt(deltat,stepno,pitchfirstordervar,meanpitang),30.d0*degrad)
! Low-pass filtering of the nacelle wind speed
WSPfilt=lowpass1orderfilt(deltat,stepno,wspfirstordervar,wsp)
! Minimum pitch angle may vary with filtered wind speed
theta_min=GetOptiPitch(WSPfilt)
switch1_pitang_lower=switch1_pitang_lower+theta_min
switch1_pitang_upper=switch1_pitang_upper+theta_min
!**************************************************************************************************
! Speed ref. changes max. <-> min. for torque controller and remains at rated for pitch controller
!**************************************************************************************************
if (omegafilt.gt.0.5d0*(omega_ref_max+omega_ref_min)) then
  omega_err_filt_speed=omegafilt-omega_ref_max
else
  omega_err_filt_speed=omegafilt-omega_ref_min
endif
!**************************************************************************************************
! PID regulation of generator torque
!**************************************************************************************************
! Limits for full load
if (const_power) then
  Qg_min_full=dmin1(Pe_rated/dmax1(omega,1.d-15),max_lss_torque)
  Qg_max_full=Qg_min_full
else
  Qg_min_full=Qg_rated
  Qg_max_full=Qg_rated
endif
! Limits for partial load that opens in both ends
ommin1=omega_ref_min
ommin2=omega_ref_min/rel_sp_open_Qg
ommax1=(2.d0*rel_sp_open_Qg-1.d0)*omega_ref_max
ommax2=rel_sp_open_Qg*omega_ref_max
x=switch_spline(omegafilt,ommin1,ommin2)
Qg_min_partial=dmin1(Kopt*omegafilt**2*x,Kopt*ommax1**2)
x=switch_spline(omegafilt,ommax1,ommax2)
Qg_max_partial=dmax1(Kopt*omegafilt**2*(1.d0-x)+Qg_max_full*x,Kopt*ommin2**2)
! Switch based on pitch
switch1=switch_spline(meanpitang,switch1_pitang_lower,switch1_pitang_upper)
switch1=lowpass1orderfilt(deltat,stepno,switchfirstordervar,switch1)
! Interpolation between partial and full load torque limits based on switch 1
PID_gen_var.outmin=(1.d0-switch1)*Qg_min_partial+switch1*Qg_min_full
PID_gen_var.outmax=(1.d0-switch1)*Qg_max_partial+switch1*Qg_max_full
if (PID_gen_var.outmin.gt.PID_gen_var.outmax) PID_gen_var.outmin=PID_gen_var.outmax
! Compute PID feedback to generator torque
kgain_torque=1.d0
Qgen_ref=PID(stepno,deltat,kgain_torque,PID_gen_var,omega_err_filt_speed)
!-------------------------------------------------------------------------------------------------
! Control of cut-in regarding generator torque
!-------------------------------------------------------------------------------------------------
if (t_cutin.gt.0.d0) then
  if (generator_cutin) then
    x=switch_spline(time,t_generator_cutin,t_generator_cutin+t_cutin_delay)
    Qgen_ref=Qgen_ref*x
  else
    Qgen_ref=0.d0
  endif
endif
!-------------------------------------------------------------------------------------------------
! Control of cut-out regarding generator torque
!-------------------------------------------------------------------------------------------------
if ((t_cutout.gt.0.d0).and.(time.gt.t_cutout)) then
  Qgen_ref=lowpass1orderfilt(deltat,stepno,torquefirstordervar,0.d0)
else
  dummy=lowpass1orderfilt(deltat,stepno,torquefirstordervar,Qgen_ref)
endif
!-------------------------------------------------------------------------------------------------
! Reference electrical power
!-------------------------------------------------------------------------------------------------
Pe_ref=Qgen_ref*omega
!-------------------------------------------------------------------------------------------------
! Active DT damping based on notch filtered of rotor speed
!-------------------------------------------------------------------------------------------------
if ((DT_damp_gain.gt.0.d0).and.(DT_damper_filt.f0.gt.0.d0)) then
  omega_dtfilt=bandpassfilt(deltat,stepno,DT_damper_filt,omega)
  if (t_cutin.gt.0.d0) then
    if (generator_cutin) then
      x=switch_spline(time,t_generator_cutin+t_cutin_delay,t_generator_cutin+2.d0*t_cutin_delay)
      Qdamp_ref=DT_damp_gain*omega_dtfilt*x
      Qgen_ref=dmin1(dmax1(Qgen_ref+Qdamp_ref,0.d0),max_lss_torque)
    endif
  else
    Qdamp_ref=DT_damp_gain*omega_dtfilt
    Qgen_ref=dmin1(dmax1(Qgen_ref+Qdamp_ref,0.d0),max_lss_torque)
  endif
endif
!**************************************************************************************************
! PID regulation of collective pitch angle
!**************************************************************************************************
! Reference speed is equal rated speed
omega_err_filt_pitch=omegafilt-omega_ref_max
! Limits
PID_pit_var.outmin=theta_min
PID_pit_var.outmax=pitch_stopang
! Aerodynamic gain scheduling
if (kk2.gt.0.d0) then
  aero_gain=1.d0+meanpitangfilt/kk1+meanpitangfilt**2/kk2
else
  aero_gain=1.d0+meanpitangfilt/kk1
endif
! Nonlinear gain to avoid large rotor speed excursion
kgain_pitch=(omega_err_filt_pitch**2/(omega_ref_max*(rel_limit-1.d0))**2+1.d0)/aero_gain
!-------------------------------------------------------------------------------------------------
! Control of cut-in regarding pitch
!-------------------------------------------------------------------------------------------------
if (t_cutin.gt.0.d0) then
  if (time.lt.t_cutin) then
    PID_pit_var.outmin=pitch_stopang
    PID_pit_var.outmax=pitch_stopang
    kgain_pitch=0.d0
    dummy=lowpass1orderfilt(deltat,stepno,cutinfirstordervar,omega-omega_ref_min)
  else
    if (.not.generator_cutin) then
      kgain_pitch(1:3,1)=0.25d0*kgain_pitch(1:3,1)
      kgain_pitch(1:3,2)=0.d0
      omega_err_filt_pitch=omegafilt-omega_ref_min
      x=lowpass1orderfilt(deltat,stepno,cutinfirstordervar,omega-omega_ref_min)
      if (dabs(x).lt.omega_ref_min*1.d-2) then
        generator_cutin=.true.
        t_generator_cutin=time
      endif
    else
      x=switch_spline(time,t_generator_cutin,t_generator_cutin+t_cutin_delay)
      omega_err_filt_pitch=omegafilt-(omega_ref_min*(1.d0-x)+omega_ref_max*x)
      kgain_pitch(1:3,1)=0.25d0*kgain_pitch(1:3,1)+kgain_pitch(1:3,1)*0.75d0*x
      kgain_pitch(1:3,2)=kgain_pitch(1:3,2)*x
    endif
  endif
endif
!-------------------------------------------------------------------------------------------------
! Control of cut-out regarding pitch
!-------------------------------------------------------------------------------------------------
if ((t_cutout.gt.0.d0).and.(time.gt.t_cutout+pitch_stopdelay)) then
  select case(pitch_stoptype)
    case(1) ! Normal 2-step stop situation
      PID_pit_var.outmax=pitch_stopang
      PID_pit_var.outmin=pitch_stopang
      if (time.gt.t_cutout+pitch_stopdelay+pitch_stopdelay2) then
        PID_pit_var.velmax=pitch_stopvelmax2
      else
        PID_pit_var.velmax=pitch_stopvelmax
      endif
    case(2) ! Exponential decay approach
      PID_pit_var.outmax=pitch_stopang
      PID_pit_var.outmin=pitch_stopang
      if ((time-(t_cutout+pitch_stopdelay))/pitch_stopdelay2.lt.10.d0) then
        PID_pit_var.velmax=pitch_stopang/pitch_stopdelay2&
                          *dexp(-(time-(t_cutout+pitch_stopdelay))/pitch_stopdelay2)
      else
        PID_pit_var.velmax=0.d0
      endif
      if (PID_pit_var.velmax.gt.pitch_stopvelmax) PID_pit_var.velmax=pitch_stopvelmax
      if (PID_pit_var.velmax.lt.pitch_stopvelmax2) PID_pit_var.velmax=pitch_stopvelmax2
    case default
      write(6,'(a,i2,a)') ' *** ERROR *** Stop type ',pitch_stoptype,' not known'
      stop
  end select
endif
!-------------------------------------------------------------------------------------------------
! Compute PID feedback to generator torque
!-------------------------------------------------------------------------------------------------
if (DT_mode_filt.f0.gt.0.d0) then
  e_pitch(1)=notch2orderfilt(deltat,stepno,DT_mode_filt,omega_err_filt_pitch)
  e_pitch(2)=notch2orderfilt(deltat,stepno,pwr_DT_mode_filt,Pe_ref-Pe_rated)
else
  e_pitch(1)=omega_err_filt_pitch
  e_pitch(2)=Pe_ref-Pe_rated
endif
theta_col_ref=PID2(stepno,deltat,kgain_pitch,PID_pit_var,e_pitch)
thetaref=theta_col_ref
!**************************************************************************************************
! Output
!**************************************************************************************************
array2( 1)=Qgen_ref             !    1: Generator torque reference            [Nm]
array2( 2)=thetaref(1)          !    2: Pitch angle reference of blade 1      [rad]
array2( 3)=thetaref(2)          !    3: Pitch angle reference of blade 2      [rad]
array2( 4)=thetaref(3)          !    4: Pitch angle reference of blade 3      [rad]
array2( 5)=Pe_ref               !    5: Power reference                       [W]
array2( 6)=WSPfilt              !    6: Filtered wind speed                   [m/s]
array2( 7)=omegafilt            !    7: Filtered rotor speed                  [rad/s]
array2( 8)=omega_err_filt_speed !    8: Filtered rotor speed error for torque [rad/s]
array2( 9)=omega_dtfilt         !    9: Bandpass filtered rotor speed         [rad/s]
array2(10)=PID_gen_var.outpro   !   10: Proportional term of torque contr.    [Nm]
array2(11)=PID_gen_var.outset   !   11: Integral term of torque controller    [Nm]
array2(12)=PID_gen_var.outmin   !   12: Minimum limit of torque               [Nm]
array2(13)=PID_gen_var.outmax   !   13: Maximum limit of torque               [Nm]
array2(14)=switch1              !   14: Torque limit switch based on pitch    [-]
array2(15)=omega_err_filt_pitch !   15: Filtered rotor speed error for pitch  [rad/s]
array2(16)=e_pitch(2)           !   16: Power error for pitch                 [W]
array2(17)=PID_pit_var.outpro   !   17: Proportional term of pitch controller [rad]
array2(18)=PID_pit_var.outset   !   18: Integral term of pitch controller     [rad]
array2(19)=PID_pit_var.outmin   !   19: Minimum limit of pitch                [rad]
array2(20)=PID_pit_var.outmax   !   20: Maximum limit of pitch                [rad]
array2(21)=Qdamp_ref            !   21: Torque reference from DT damper       [Nm]
return
end subroutine update_regulation
!**************************************************************************************************
\end{verbatim}
}

\pagebreak
\section{Functional routines in risoe\_controller\_fcns.f90}
{\scriptsize
\begin{verbatim}
module risoe_controller_fcns
! Constants
real*8 pi,degrad,raddeg
parameter(pi=3.14159265358979,degrad=0.0174532925,raddeg=57.2957795131)
integer*4 maxwplines
parameter(maxwplines=100)
! Types
type Tfirstordervar
  real*8 tau,x1,x1_old,y1,y1_old
  integer*4 stepno1
end type Tfirstordervar
type Tlowpass2order
  real*8 zeta,f0,x1,x2,x1_old,x2_old,y1,y2,y1_old,y2_old
  integer*4 stepno1
end type Tlowpass2order
type Tnotch2order
  real*8::zeta1=0.1
  real*8::zeta2=0.001
  real*8 f0,x1,x2,x1_old,x2_old,y1,y2,y1_old,y2_old
  integer*4 stepno1
end type Tnotch2order
type Tbandpassfilt
  real*8::zeta=0.02
  real*8::tau=0.0
  real*8 f0,x1,x2,x1_old,x2_old,y1,y2,y1_old,y2_old
  integer*4 stepno1
end type Tbandpassfilt
type Tpidvar
  real*8 Kpro,Kdif,Kint,outmin,outmax,velmax,error1,outset1,outres1
  integer*4 stepno1
  real*8 outset,outpro,outdif,error1_old,outset1_old,outres1_old,outres
end type Tpidvar
type Tpid2var
  real*8 Kpro(2),Kdif(2),Kint(2),outmin,outmax,velmax,error1(2),outset1,outres1
  integer*4 stepno1
  real*8 outset,outpro,outdif,error1_old(2),outset1_old,outres1_old,outres
end type Tpid2var
type Twpdata
  real*8 wpdata(maxwplines,2)
  integer*4 lines
end type Twpdata
! Variables
integer*4 stepno
logical const_power
real*8 deltat,time_old
real*8 omega_ref_max,omega_ref_min,Pe_rated,Qg_rated,pitch_stopang,max_lss_torque
real*8 Kopt,rel_sp_open_Qg
real*8 kk1,kk2,rel_limit
real*8 switch1_pitang_lower,switch1_pitang_upper,switch1
real*8 DT_damp_gain
logical::generator_cutin=.false.
real*8 t_cutin,t_generator_cutin,t_cutin_delay
integer*4 pitch_stoptype
real*8 t_cutout,pitch_stopdelay,pitch_stopdelay2,pitch_stopvelmax,pitch_stopvelmax2
type(Tlowpass2order) omega2ordervar
type(Tnotch2order) DT_mode_filt
type(Tnotch2order) pwr_DT_mode_filt
type(Tbandpassfilt) DT_damper_filt
type(Tpid2var) PID_pit_var
type(Tpidvar) PID_gen_var
type(Twpdata) OPdatavar
type(Tfirstordervar) wspfirstordervar
type(Tfirstordervar) pitchfirstordervar
type(Tfirstordervar) torquefirstordervar
type(Tfirstordervar) switchfirstordervar
type(Tfirstordervar) cutinfirstordervar
!**************************************************************************************************
contains
!**************************************************************************************************
function switch_spline(x,x0,x1)
implicit none
! A function that goes from 0 at x0 to 1 at x1
real*8 switch_spline,x,x0,x1
if (x0.ge.x1) then
  if (x.lt.x0) then
    switch_spline=0.d0
  else
    switch_spline=1.d0
  endif
elseif (x0.gt.x1) then
  switch_spline=0.d0
else
  if (x.lt.x0) then
    switch_spline=0.d0
  elseif (x.gt.x1) then
    switch_spline=1.d0
  else
    switch_spline=2.d0/(-x1**3-3.d0*x1*x0**2+x0**3+3.d0*x0*x1**2)*x**3&
                 -3.d0*(x0+x1)/(-x1+x0)/(x1**2-2.d0*x1*x0+x0**2)*x**2&
                 +6.d0*x1*x0/(-x1+x0)/(x1**2-2.d0*x1*x0+x0**2)*x&
                 +(x0-3.d0*x1)*x0**2/(-x1+x0)/(x1**2-2*x1*x0+x0**2)
  endif
endif
return
end function switch_spline
!**************************************************************************************************
function interpolate(x,x0,x1,f0,f1)
implicit none
real*8 interpolate,x,x0,x1,f0,f1
if (x0.eq.x1) then
  interpolate=f0
else
  interpolate=(x-x1)/(x0-x1)*f0+(x-x0)/(x1-x0)*f1
endif
return
end function interpolate
!**************************************************************************************************
function GetOptiPitch(wsp)
implicit none
real*8 GetOptiPitch,wsp
! local vars
real*8 x,x0,x1,f0,f1,pitch
integer*4 i
i=1
do while((OPdatavar.wpdata(i,1).le.wsp).and.(i.le.OPdatavar.lines))
  i=i+1
enddo
if (i.eq.1) then
  GetOptiPitch=OPdatavar.wpdata(1,2)
elseif (i.gt.OPdatavar.lines) then
  GetOptiPitch=OPdatavar.wpdata(OPdatavar.lines,2)
else
  x=wsp
  x0=OPdatavar.wpdata(i-1,1)
  x1=OPdatavar.wpdata(i,1)
  f0=OPdatavar.wpdata(i-1,2)
  f1=OPdatavar.wpdata(i,2)
  Pitch=interpolate(x,x0,x1,f0,f1)
  GetOptiPitch=Pitch
endif
return
end function GetOptiPitch
!**************************************************************************************************
function lowpass1orderfilt(dt,stepno,filt,x)
implicit none
integer*4 stepno
real*8 lowpass1orderfilt,dt,x,y,a1,b1,b0,tau
type(Tfirstordervar) filt
! Step
if ((stepno.eq.1).and.(stepno.gt.filt.stepno1)) then
  filt.x1_old=x
  filt.y1_old=x
  y=x
else
  if (stepno.gt.filt.stepno1) then
    filt.x1_old=filt.x1
    filt.y1_old=filt.y1
  endif
  tau=filt.tau
  a1 = (2 * tau - dt) / (2 * tau + dt)
  b0 = dt / (2 * tau + dt)
  b1 = b0
  y=a1*filt.y1_old+b0*x+b1*filt.x1_old
endif
! Save previous values
filt.x1=x
filt.y1=y
filt.stepno1=stepno
! Output
lowpass1orderfilt=y
return
end function lowpass1orderfilt
!**************************************************************************************************
function lowpass2orderfilt(dt,stepno,filt,x)
implicit none
real*8 lowpass2orderfilt(2),dt,x
integer*4 stepno
type(Tlowpass2order) filt
! local vars
real*8 y,f0,zeta,a1,a2,b0,b1,b2,denom
! Step
if ((stepno.eq.1).and.(stepno.gt.filt.stepno1)) then
  filt.x1=x
  filt.x2=x
  filt.x1_old=filt.x1
  filt.x2_old=filt.x2
  filt.y1=x
  filt.y2=x
  filt.y1_old=filt.y1
  filt.y2_old=filt.y2
  y=x
else
  if (stepno.gt.filt.stepno1) then
    filt.x1_old=filt.x1
    filt.x2_old=filt.x2
    filt.y1_old=filt.y1
    filt.y2_old=filt.y2
  endif
  f0=filt.f0
  zeta=filt.zeta
  denom=3.d0+6.d0*zeta*pi*f0*dt+4.d0*pi**2*f0**2*dt**2
  a1=(6.d0-4.d0*pi**2*f0**2*dt**2)/denom
  a2=(-3.d0+6.d0*zeta*pi*f0*dt-4.d0*pi**2*f0**2*dt**2)/denom
  b0=4.d0*pi**2*f0**2*dt**2/denom
  b1=b0
  b2=b0
  y=a1*filt.y1_old+a2*filt.y2_old+b0*x+b1*filt.x1_old+b2*filt.x2_old
endif
! Save previous values
filt.x2=filt.x1
filt.x1=x
filt.y2=filt.y1
filt.y1=y
filt.stepno1=stepno
! Output
lowpass2orderfilt(1)=y
lowpass2orderfilt(2)=0.5d0*(y-filt.y2_old)/dt
return
end function lowpass2orderfilt
!**************************************************************************************************
function notch2orderfilt(dt,stepno,filt,x)
implicit none
real*8 notch2orderfilt,dt,x
integer*4 stepno
type(Tnotch2order) filt
! local vars
real*8 y,f0,zeta1,zeta2,a1,a2,b0,b1,b2,denom
! Step
if ((stepno.eq.1).and.(stepno.gt.filt.stepno1)) then
  filt.x1=x
  filt.x2=x
  filt.x1_old=filt.x1
  filt.x2_old=filt.x2
  filt.y1=x
  filt.y2=x
  filt.y1_old=filt.y1
  filt.y2_old=filt.y2
  y=x
else
  if (stepno.gt.filt.stepno1) then
    filt.x1_old=filt.x1
    filt.x2_old=filt.x2
    filt.y1_old=filt.y1
    filt.y2_old=filt.y2
  endif
  f0=filt.f0
  zeta1=filt.zeta1
  zeta2=filt.zeta2
  denom=3.d0+6.d0*zeta1*pi*f0*dt+4.d0*pi**2*f0**2*dt**2
  a1=(6.d0-4.d0*pi**2*f0**2*dt**2)/denom
  a2=(-3.d0+6.d0*zeta1*pi*f0*dt-4.d0*pi**2*f0**2*dt**2)/denom
  b0=(3.d0+6.d0*zeta2*pi*f0*dt+4.d0*pi**2*f0**2*dt**2)/denom
  b1=(-6.d0+4.d0*pi**2*f0**2*dt**2)/denom
  b2=(3.d0-6.d0*zeta2*pi*f0*dt+4.d0*pi**2*f0**2*dt**2)/denom
  y=a1*filt.y1_old+a2*filt.y2_old+b0*x+b1*filt.x1_old+b2*filt.x2_old
endif
! Save previous values
filt.x2=filt.x1
filt.x1=x
filt.y2=filt.y1
filt.y1=y
filt.stepno1=stepno
! Output
notch2orderfilt=y
return
end function notch2orderfilt
!**************************************************************************************************
function bandpassfilt(dt,stepno,filt,x)
implicit none
real*8 bandpassfilt,dt,x
integer*4 stepno
type(Tbandpassfilt) filt
! local vars
real*8 y,f0,zeta,tau,a1,a2,b0,b1,b2,denom
! Step
if ((stepno.eq.1).and.(stepno.gt.filt.stepno1)) then
  filt.x1=x
  filt.x2=x
  filt.x1_old=filt.x1
  filt.x2_old=filt.x2
  filt.y1=x
  filt.y2=x
  filt.y1_old=filt.y1
  filt.y2_old=filt.y2
  y=x
else
  if (stepno.gt.filt.stepno1) then
    filt.x1_old=filt.x1
    filt.x2_old=filt.x2
    filt.y1_old=filt.y1
    filt.y2_old=filt.y2
  endif
  f0=filt.f0
  zeta=filt.zeta
  tau=filt.tau
  denom=3.d0+6.d0*zeta*pi*f0*dt+4.d0*pi**2*f0**2*dt**2
  a1=-(-6.d0+4.d0*pi**2*f0**2*dt**2)/denom
  a2=-(3.d0-6.d0*zeta*pi*f0*dt+4.d0*pi**2*f0**2*dt**2)/denom
  b0=-(-6.d0*zeta*pi*f0*dt-12.d0*zeta*pi*f0*tau)/denom
  b1=-24.d0*zeta*pi*f0*tau/denom
  b2=-(6.d0*zeta*pi*f0*dt-12.d0*zeta*pi*f0*tau)/denom
  y=a1*filt.y1_old+a2*filt.y2_old+b0*x+b1*filt.x1_old+b2*filt.x2_old
endif
! Save previous values
filt.x2=filt.x1
filt.x1=x
filt.y2=filt.y1
filt.y1=y
filt.stepno1=stepno
! Output
bandpassfilt=y
return
end function bandpassfilt
!**************************************************************************************************
function PID(stepno,dt,kgain,PIDvar,error)
implicit none
integer*4 stepno
real*8 PID,dt,kgain(3),error
type(Tpidvar) PIDvar
! Local vars
real*8 eps
parameter(eps=1.d-6)
! Initiate
if (stepno.eq.1) then
  PIDvar.outset1=0
  PIDvar.outres1=0
  PIDvar.error1=0
  PIDvar.error1_old=0.0
  PIDvar.outset1_old=0.0
  PIDvar.outres1_old=0.0
endif
! Save previous values
if (stepno.gt.PIDvar.stepno1) then
  PIDvar.outset1_old=PIDvar.outset1
  PIDvar.outres1_old=PIDvar.outres1
  PIDvar.error1_old=PIDvar.error1
endif
! Update the integral term
PIDvar.outset=PIDvar.outset1_old+0.5d0*(error+PIDvar.error1)*Kgain(2)*PIDvar.Kint*dt
! Update proportional term
PIDvar.outpro=Kgain(1)*PIDvar.Kpro*0.5d0*(error+PIDvar.error1)
! Update differential term
PIDvar.outdif=Kgain(3)*PIDvar.Kdif*(error-PIDvar.error1_old)/dt
! Sum to up
PIDvar.outres=PIDvar.outset+PIDvar.outpro+PIDvar.outdif
! Satisfy hard limits
if (PIDvar.outres.lt.PIDvar.outmin) then
  PIDvar.outres=PIDvar.outmin
elseif (PIDvar.outres.gt.PIDvar.outmax) then
  PIDvar.outres=PIDvar.outmax
endif
! Satisfy max velocity
if (PIDvar.velmax.gt.eps) then
    if ((abs(PIDvar.outres-PIDvar.outres1_old)/dt).gt.PIDvar.velmax) &
      PIDvar.outres=PIDvar.outres1_old+dsign(PIDvar.velmax*dt,PIDvar.outres-PIDvar.outres1_old)
endif
! Anti-windup on integral term and save results
PIDvar.outset1=PIDvar.outres-PIDvar.outpro-PIDvar.outdif
PIDvar.outres1=PIDvar.outres
PIDvar.error1=error
PIDvar.stepno1=stepno
! Set output
if (stepno.eq.0) then
  PID=0
else
  PID=PIDvar.outres
endif
return
end function PID
!**************************************************************************************************
function PID2(stepno,dt,kgain,PIDvar,error)
implicit none
integer*4 stepno
real*8 PID2,dt,kgain(3,2),error(2)
type(Tpid2var) PIDvar
! Local vars
real*8 eps
parameter(eps=1.d-6)
! Initiate
if (stepno.eq.1) then
  PIDvar.outset1=0
  PIDvar.outres1=0
  PIDvar.error1=0
  PIDvar.error1_old=0.0
  PIDvar.outset1_old=0.0
  PIDvar.outres1_old=0.0
endif
! Save previous values
if (stepno.gt.PIDvar.stepno1) then
  PIDvar.outset1_old=PIDvar.outset1
  PIDvar.outres1_old=PIDvar.outres1
  PIDvar.error1_old=PIDvar.error1
endif
! Update the integral term
PIDvar.outset=PIDvar.outset1_old+0.5d0*dt*(Kgain(2,1)*PIDvar.Kint(1)*(error(1)+PIDvar.error1(1))&
                                          +Kgain(2,2)*PIDvar.Kint(2)*(error(2)+PIDvar.error1(2)))
! Update proportional term
PIDvar.outpro=0.5d0*(Kgain(1,1)*PIDvar.Kpro(1)*(error(1)+PIDvar.error1(1))&
                    +Kgain(1,2)*PIDvar.Kpro(2)*(error(2)+PIDvar.error1(2)))
! Update differential term
PIDvar.outdif=(Kgain(3,1)*PIDvar.Kdif(1)*(error(1)-PIDvar.error1_old(1))&
              +Kgain(3,2)*PIDvar.Kdif(2)*(error(2)-PIDvar.error1_old(2)))/dt
! Sum to up
PIDvar.outres=PIDvar.outset+PIDvar.outpro+PIDvar.outdif
! Satisfy hard limits
if (PIDvar.outres.lt.PIDvar.outmin) then
  PIDvar.outres=PIDvar.outmin
elseif (PIDvar.outres.gt.PIDvar.outmax) then
  PIDvar.outres=PIDvar.outmax
endif
! Satisfy max velocity
if (PIDvar.velmax.gt.eps) then
    if ((abs(PIDvar.outres-PIDvar.outres1_old)/dt).gt.PIDvar.velmax) &
      PIDvar.outres=PIDvar.outres1_old+dsign(PIDvar.velmax*dt,PIDvar.outres-PIDvar.outres1_old)
endif
! Anti-windup on integral term and save results
PIDvar.outset1=PIDvar.outres-PIDvar.outpro-PIDvar.outdif
PIDvar.outres1=PIDvar.outres
PIDvar.error1=error
PIDvar.stepno1=stepno
! Set output
if (stepno.eq.0) then
  PID2=0
else
  PID2=PIDvar.outres
endif
return
end function PID2
!**************************************************************************************************
end module risoe_controller_fcns
\end{verbatim}
}
